# U-Boot: Orangepi PC2
# Maintainer: Kevin Mihelich <kevin@archlinuxarm.org>

buildarch=8

pkgname=uboot-orangepi-pc2
pkgver=2019.07
pkgrel=1
pkgdesc="U-Boot for Orangepi PC2"
arch=('aarch64')
url='http://www.denx.de/wiki/U-Boot/WebHome'
license=('GPL')
backup=('boot/boot.txt' 'boot/boot.scr')
makedepends=('bc' 'dtc' 'git')
install=${pkgname}.install
source=("ftp://ftp.denx.de/pub/u-boot/u-boot-${pkgver/rc/-rc}.tar.bz2"
        'https://github.com/ARM-software/arm-trusted-firmware/archive/v2.1.tar.gz'
        '0001-ATF-set-fno-stack-protector.patch'
        'boot.txt'
        'mkscr')
md5sums=('73434338536c7500b4302bd0a02921ed'
         '75c8f4958fb493d9bd7a8e5a9636ec18'
         '2d895b972dee8be2bfefb08813644e0d'
         '2272ee7f35e7d0bf82f92f500099a320'
         '021623a04afd29ac3f368977140cfbfd')

prepare() {
  cd arm-trusted-firmware-2.1
  git apply ../0001-ATF-set-fno-stack-protector.patch
}

build() {
  unset CFLAGS CXXFLAGS CPPFLAGS LDFLAGS

  cd arm-trusted-firmware

  make PLAT=sun50i_a64 DEBUG=1 bl31
  cp build/sun50i_a64/debug/bl31.bin ../u-boot-${pkgver/rc/-rc}

  cd ../u-boot-${pkgver/rc/-rc}

  make distclean
  make orangepi_pc2_defconfig
  echo 'CONFIG_IDENT_STRING=" Arch Linux ARM"' >> .config
  make -j$(nproc) EXTRAVERSION=-${pkgrel}
}

package() {
  cd u-boot-${pkgver/rc/-rc}

  mkdir -p "${pkgdir}"/boot

  cat spl/sunxi-spl.bin u-boot.itb > u-boot-sunxi-with-spl.bin
  cp ../boot.txt ../mkscr u-boot-sunxi-with-spl.bin "${pkgdir}"/boot
  tools/mkimage -A arm -O linux -T script -C none -n "U-Boot boot script" -d "${pkgdir}"/boot/boot.txt "${pkgdir}"/boot/boot.scr
}
